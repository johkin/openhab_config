import org.joda.time.*
import org.openhab.core.library.types.DateTimeType
import org.openhab.core.library.items.SwitchItem


rule "Tänd utebelysning eftermiddag"
when
	Item Sunset_Event changed to ON
then
	gOutsideLights.allMembers.forEach[light| light.sendCommand(ON)]
end

rule "Tänd utebelysning morgon"
when
	Time cron "0 0 6 ? * MON-FRI" or
	Time cron "0 0 7 ? * SAT-SUN"
then
	val SunriseTime = Sunrise_Time.state as DateTimeType
    val jodaSunriseTime = new DateTime(SunriseTime.calendar.timeInMillis)
    
	if (jodaSunriseTime.afterNow) {
		gOutsideLights.allMembers.forEach[light| light.sendCommand(ON)]	
	}
end


rule "Släck utebelysning morgon"
when
	Item Sunrise_Event changed to ON
then
	gOutsideLights.allMembers.forEach[light| light.sendCommand(OFF)]
end

rule "Släck utebelysning kväll"
when
	Time cron "0 30 22 ? * MON-THU,SUN" or
	Time cron "0 30 23 ? * FRI-SAT"
then
	gOutsideLights.allMembers.forEach[light| light.sendCommand(OFF)]
end

rule "Tänd utebelysning vid rörelse"
when
	Item BackSide_Motion_Sensor changed
then
	if (BackSide_Light_Sensor.state < 50) {
		if(BackSide_Motion_Sensor.state == OPEN) {
			gOutsideLights.allMembers.forEach[light| light.sendCommand(ON)]
		} else {
			gOutsideLights.allMembers.forEach[light| light.sendCommand(OFF)]
		}	
	}
end