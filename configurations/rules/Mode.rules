import org.joda.time.*
import org.openhab.model.script.actions.Timer
import org.openhab.core.library.types.DecimalType
import org.openhab.core.library.types.DateTimeType
import org.openhab.core.library.items.SwitchItem

rule "Initiering"
when
	System started
then

	if (now.getHourOfDay() > 5 || now.getHourOfDay() < 22) {
		logDebug("mode", "Ändrar läge till dag.")
		sendCommand(Mode, 0)
	} else if (Mode.state == 2 || Mode.state == 3) {
		logDebug("mode", "Ändrar läge till natt.")
		sendCommand(Mode, 1)
	}

end


rule "Registrera rörelse"
when
	Item gMotionSensorIndoor changed
then

	val listOfMotionSensorsTripped = gMotionSensorIndoor.allMembers
		.filter([sensor | sensor.state == CLOSED])
		
	logDebug("mode", "Antal öppna sensorer {}", listOfMotionSensorsTripped.size)
		
	if (!listOfMotionSensorsTripped.empty) {
		logDebug("mode", "Minst en sensor är Open, justerar läge.")
		
		if (now.getHourOfDay() > 5 && now.getHourOfDay() < 22) {
			logDebug("mode", "Ändrar läge till dag.")
			sendCommand(Mode, 0)
		} else if (Mode.state == 2 || Mode.state == 3) {
			logDebug("mode", "Ändrar läge till natt.")
			sendCommand(Mode, 1)
		}
	}
end

rule "Kontrollera senaste rörelse"
when
	Time cron "0 */15 * ? * *"
then
	logDebug("mode", "Antal sensorer {}", gMotionSensorIndoor.members.size)
	logDebug("mode", "Antal sensorer {}", gMotionSensorIndoor.allMembers.size)
	
	val Period period = new Period().withMinutes(30)

	val listOfMotionSensorsNotTripped = gMotionSensorIndoor.allMembers
		.map([sensor | 
			logDebug("mode", "Senaste rörelse {}, {}", sensor.name, sensor.lastUpdate)
			new DateTime(sensor.lastUpdate)
		])
		.filter([lastUpdate | lastUpdate.plus(period).afterNow])
		
	logDebug("mode", "Antal sensorer som inte rört sig sedan {},  {}", now.minus(period), listOfMotionSensorsNotTripped.size)
		
	if (listOfMotionSensorsNotTripped.empty) {
		if (Mode.state == 0) {
			if (now.getHourOfDay() > 22 || now.getHourOfDay() < 5) {
				logDebug("mode", "Ändrar läge till natt.")
				sendCommand(Mode, 1)
			} else {
				logDebug("mode", "Ändrar läge till borta.")
				sendCommand(Mode, 2)
			}
		} 
	}
end

rule "Kontrollera senaste lägesändring"
when
	Time cron "0 0 7,19 ? * *"
then
	logDebug("mode", "Senaste ändring av läge {}", Mode.lastUpdate)
	if (Mode.state != 3) {
		if (Mode.lastUpdate.before(now.minusDays(1).toDate)) {
			logDebug("mode", "Ändrar läge till semester.")
			sendCommand(Mode, 3)
		}	
	}
end