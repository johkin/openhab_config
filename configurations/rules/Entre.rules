import org.joda.time.*
import org.openhab.model.script.actions.Timer
import org.openhab.core.library.types.DecimalType
import org.openhab.core.library.types.DateTimeType
import org.openhab.core.library.items.SwitchItem

val Integer nightLevel = 10
val Integer dayLevelHigh = 100
val Integer dayLevelLow = 25
val Integer minutesHighOn = 10
val Integer minutesLowOn = 120
val Integer minLightLevel = 90

var Integer level

rule "Entre, Initiering"
when
	System started
then
	level = switch (Mode.state) {
		case 0: dayLevelHigh  
		case 1: nightLevel
		case 2: dayLevelLow
		default: dayLevelHigh
	}
end	

rule "Entre, Släck med schema"
when
	Time cron "0 */5 * ? * *"
then
	if (Entre_Motion_Sensor.state == CLOSED && Entre_Dimmer.state != 0) {
		logDebug("entre", "Kontrollera senaste rörelse {}", Entre_Motion_Sensor.lastUpdate)
		val lastUpdate = new DateTime(Entre_Motion_Sensor.lastUpdate)
		
		val Period offPeriod = switch Mode.state {
			case 0 : new Period().withMinutes(minutesLowOn)
			case 1 : new Period().withMinutes(minutesHighOn)
			case 2 : new Period().withMinutes(minutesLowOn)
			default : new Period().withMinutes(minutesLowOn)
		}
		val Period decreasePeriod = switch Mode.state {
			case 0 : new Period().withMinutes(minutesHighOn)
			default : null
		}
		if (lastUpdate.plus(offPeriod).beforeNow) {
			logDebug("entre", "Släcker belysning")
			sendCommand(Entre_Dimmer, OFF)	
		} else if (decreasePeriod != null && lastUpdate.plus(decreasePeriod).beforeNow) {
			if (Entre_Dimmer.state > dayLevelLow) {
				logDebug("entre", "Sänker belysning till {}", dayLevelLow)
				sendCommand(Entre_Dimmer, dayLevelLow)	
			}
		}	
	}
end

rule "Entre, Tänd entre vid rörelse"
when
	Item Entre_Motion_Sensor changed to OPEN
then
	logDebug("entre", "Entre_Motion_Sensor.state {}", Entre_Motion_Sensor.state)
	logDebug("entre", "Entre_Dimmer.state {}", Entre_Dimmer.state)
	if (Entre_Light_Sensor.state <= minLightLevel) {
		if (Mode.state == 0 && Entre_Dimmer.state == dayLevelLow) {
			level == dayLevelHigh
			logDebug("entre", "Ökar belysning: {}", level)
			sendCommand(Entre_Dimmer, level)
		} else if (Entre_Dimmer.state == 0){
			logDebug("entre", "Tänder belysning: {}", level)
			sendCommand(Entre_Dimmer, level)	
		}	
	}
end

rule "Entre, Tänd entre vid mörker"
when
	Item Entre_Light_Sensor changed
then
	logDebug("entre", "Entre_Light_Sensor.state {}", Entre_Light_Sensor.state)
	logDebug("entre", "Entre_Motion_Sensor.state {}", Entre_Motion_Sensor.state)
	logDebug("entre", "Entre_Dimmer.state {}", Entre_Dimmer.state)
	if (Entre_Light_Sensor.state <= minLightLevel) {
		if (Entre_Motion_Sensor.state == OPEN) {
			if (Mode.state == 0 && Entre_Dimmer.state == dayLevelLow) {
				level == dayLevelHigh
				logDebug("entre", "Ökar belysning: {}", level)
				sendCommand(Entre_Dimmer, level)
			} else if (Entre_Dimmer.state == 0){
				logDebug("entre", "Tänder belysning: {}", level)
				sendCommand(Entre_Dimmer, level)	
			}	
		}
	} else {
			
	}
end

rule "Entre, Justera ljusstyrka vid ändring av läge"
when
	Item Mode changed
then
	logDebug("entre", "Justera ljusstyrka Mode.state {}", Mode.state)
	level = switch (Mode.state) {
		case 0: dayLevelHigh  
		case 1: nightLevel
		case 2: dayLevelLow
		case 3: dayLevelHigh
	}
	logDebug("entre", "Justera ljusstyrka level {}", level)
	logDebug("entre", "Justera ljusstyrka Entre_Dimmer.state {}", Entre_Dimmer.state)
	if (Entre_Dimmer.state != 0) {
		sendCommand(Entre_Dimmer, level)	
	}
end

rule "Entre, Lagra nivå vid ändring"
when 
	Item Entre_Dimmer changed
then
	
	logDebug("entre", "Entre_Dimmer.state {}", Entre_Dimmer.state)
	if (Entre_Dimmer.state != 0 && Entre_Dimmer.state != dayLevelLow) {
		level = (Entre_Dimmer.state as DecimalType).intValue
		logDebug("entre", "Stored new dimmer level {}", level)
	} 
end